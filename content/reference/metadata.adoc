= Metadata
Rich Hickey
2015-01-01
:type: reference
:toc: macro
:icons: font
:prevpagehref: protocols
:prevpagetitle: Protocols
:nextpagehref: namespaces
:nextpagetitle: Namespaces

ifdef::env-github,env-browser[:outfilesuffix: .adoc]

toc::[]

Symbols and collections support metadata, a map of data _about_ the symbol or collection. The metadata system allows for arbitrary annotation of data. It is used to convey information to the compiler about types, but can also be used by application developers for many purposes, annotating data sources, policy etc.

An important thing to understand about metadata is that it is not considered to be part of the value of an object. As such, _metadata does not impact equality (or hash codes)_. Two objects that differ only in metadata are equal.

That said, metadata and its relationship to an object is immutable - an object with different metadata is a different object. One consequence of this is that applying metadata to a lazy sequence will realize the head of the sequence so that both objects can share the same sequence.

''''

=== (_meta_ obj)

Returns the metadata of obj, returns nil if there is no metadata.

[source,clojure]
----
(pprint (meta #'+)) ;; #'+ is the + var

;; {:added "1.2",
;;  :name +,
;;  :file "clojure/core.clj",
;;  :column 1,
;;  :line 984,
;;  :arglists ([] [x] [x y] [x y & more]),
;;  ...
----

=== (_with-meta_ obj map)

Returns an object of the same type and value as obj, with map as its metadata.

[source,clojure]
----
(def m ^:hi [1 2 3])
(meta (with-meta m {:bye true}))
;; {:bye true}
----

=== _pass:[*print-meta*]_

If set to logical true, when printing an object, its metadata will also be printed in a form that can be read back by the reader.

[source,clojure]
----
(def m ^:hi [1 2 3])
(binding [*print-meta* true]
  (prn m))

;; ^{:hi true} [1 2 3]
----

=== (_vary-meta_ obj f & args)

Returns an object of the same type and value as obj, with `(apply f (meta obj) args)` as its metadata.

[source,clojure]
----
(def m ^:hi [1 2 3])
(meta (vary-meta m merge {:bye true}))
;; {:hi true, :bye true}
----

=== (_alter-meta!_ ref f & args) and (_reset-meta!_ ref map)

Modify or reset the metadata respectively for a namespace/var/ref/agent/atom.

== Metadata Reader

In addition to `with-meta`, there is reader support (<<reader#metadata,Metadata Reader>>) for applying metadata to the expression following it at read-time.

See <<java_interop#typehints,Type Hints>> and <<java_interop#paramtags,param-tags>> for more information on how metadata is used by the compiler for method overload selection.


