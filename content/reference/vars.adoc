= Vars and the Global Environment
Rich Hickey
2016-01-14
:type: reference
:toc: macro
:icons: font
:navlinktext: Vars and Environments
:prevpagehref: libs
:prevpagetitle: Libs
:nextpagehref: refs
:nextpagetitle: Refs and Transactions

ifdef::env-github,env-browser[:outfilesuffix: .adoc]

toc::[]

Clojure is a practical language that recognizes the occasional need to maintain a persistent reference to a changing value and provides 4 distinct mechanisms for doing so in a controlled manner - Vars, <<refs#,Refs>>, <<agents#,Agents>> and <<atoms#,Atoms>>. Vars provide a mechanism to refer to a mutable storage location that can be dynamically rebound (to a new storage location) on a per-thread basis. Every Var can (but needn't) have a root binding, which is a binding that is shared by all threads that do not have a per-thread binding. Thus, the value of a Var is the value of its per-thread binding, or, if it is not bound in the thread requesting the value, the value of the root binding, if any.

The special form _**def**_ creates (and <<vars#Interning,interns>>) a Var. If the Var did not already exist and no initial value is supplied, the var is unbound:

[source,clojure]
----
user=> (def x)
.'user/x
user=> x
java.lang.IllegalStateException: Var user/x is unbound.
----

Supplying an initial value binds the root (even if it was already bound).

[source,clojure]
----
user=> (def x 1)
.'user/x

user=> x
1
----

By default Vars are static, but Vars can be marked as dynamic to allow per-thread bindings via the macro http://clojure.github.com/clojure/clojure.core-api.html#clojure.core/binding[binding]. Within each thread they obey a stack discipline:

[source,clojure]
----
user=> (def ^:dynamic x 1)
user=> (def ^:dynamic y 1)
user=> (+ x y)
2

user=> (binding [x 2 y 3]
         (+ x y))
5

user=> (+ x y)
2
----

Bindings created with _**binding**_ cannot be seen by any other thread. Likewise, bindings created with _**binding**_ can be assigned to, which provides a means for a nested context to communicate with code before it on the call stack. This capability is opt-in only by setting a metadata tag: _**dynamic**_ to true as in the code block above. There are scenarios that one might wish to redefine static Vars within a context and Clojure (since version 1.3) provides the functions http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/with-redefs[with-redefs] and http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/with-redefs-fn[with-redefs-fn] for such purposes.

Functions defined with http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/defn[defn] are stored in Vars, allowing for the re-definition of functions in a running program. This also enables many of the possibilities of aspect- or context-oriented programming. For instance, you could wrap a function with logging behavior only in certain call contexts or threads.

[[set]]
== (*set!* var-symbol expr)

Assignment special form.

When the first operand is a symbol, it must resolve to a global var. The value of the var's current thread binding is set to the value of expr. Currently, it is an error to attempt to set the root binding of a var using set!, i.e. var assignments are thread-local. In all cases the value of expr is returned.

Note - _you cannot assign to function params or local bindings. Only Java fields, Vars, Refs and Agents are mutable in Clojure_.

Using <<java_interop#set,set>> for Java fields is documented in <<java_interop#set,Java Interop>>.

[[Interning]]
== Interning

The Namespace system maintains global maps of symbols to Var objects _(see <<namespaces#,Namespaces>>)_. If a _**def**_ expression does not find an interned entry in the current namespace for the symbol being def-ed, it creates one, otherwise it uses the existing Var. This find-or-create process is called interning. This means that, unless they have been unmap-ed, Var objects are stable references and need not be looked up every time. It also means that namespaces constitute a global environment in which, as described in <<evaluation#,Evaluation>>, the compiler attempts to resolve all free symbols as Vars.

The <<special_forms#var,var>> special form or the `pass:[#']` reader macro _(see <<reader#,Reader>>)_ can be used to get an interned Var object instead of its current value.

[[local-vars]]
== Non-interned Vars

It is possible to create vars that are not interned by using http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/with-local-vars[with-local-vars]. These vars will not be found during free symbol resolution, and their values have to be accessed manually. But they can serve as useful thread-local mutable cells.

[[related]]
== Related functions

[%hardbreaks]
Variants of _**def**_: http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/defn[defn] http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/defn-[defn-] http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/definline[definline] http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/defmacro[defmacro] http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/defmethod[defmethod] http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/defmulti[defmulti] http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/defonce[defonce] http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/defstruct[defstruct]
Working with interned Vars: http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/declare[declare] http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/intern[intern] http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/binding[binding] http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/find-var[find-var] <<special_forms#var#,var>>
Working with Var objects: http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/with-local-vars[with-local-vars] http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/var-get[var-get] http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/var-set[var-set] http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/alter-var-root[alter-var-root] http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/var?[var?] http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/with-redefs[with-redefs] http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/with-redefs-fn[with-redefs-fn]
Var validators: http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/set-validator[set-validator] http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/get-validator[get-validator]
Common Var metadata: http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/doc[doc] http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/find-doc[find-doc] http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/test[test]
